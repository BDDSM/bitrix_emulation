&НаСервере
Перем ОбработкаОбъект;

&НаСервере
//инициализация модуля и его экспортных функций
Функция МодульОбъекта()

	Если ОбработкаОбъект=Неопределено Тогда
		ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	КонецЕсли;	
	
	Возврат ОбработкаОбъект;
	
КонецФункции

&НаКлиенте
Процедура ПрочитатьКаталог(Команда)
	ОбновитьКаталог(ФайлКаталога);
КонецПроцедуры

&НаКлиенте
Процедура ФайлКаталогаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДВ = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДВ.Фильтр = "xml|*.xml";
	ДВ.ПолноеИмяФайла = ФайлКаталога;
	Если ДВ.Выбрать() Тогда
		ФайлКаталога = ДВ.ПолноеИмяФайла;
		ОбновитьКаталог(ДВ.ПолноеИмяФайла);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКаталог(ИмяФайла)
	
	ДД = Новый ДвоичныеДанные(ИмяФайла);
	АдресХранилищаФайла = ПоместитьВоВременноеХранилище(ДД,ЭтаФорма);
	ОбновитьКаталогНаСервере(АдресХранилищаФайла);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКаталогНаСервере(АдресХранилищаФайла)
	
	ИмяВремФайла = ПолучитьИмяВременногоФайла();//или уже напрямую 
	ДД = ПолучитьИзВременногоХранилища(АдресХранилищаФайла);
	ДД.Записать(ИмяВремФайла);
	
	Результат = МодульОбъекта().РазобратьXMLВТаблицыЗначений(ИмяВремФайла);
	
	_ДеревоКаталога = Результат.Каталоги;
	
	ИзменитьКолонки(_ДеревоКаталога,"ДеревоКаталога","ДеревоКаталога",Элементы.ГруппаГруппы);
	ИзменитьКолонки(Результат.ТаблицаТоваров,"Товары","Товары",Элементы.ГруппаТовары);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьКолонки(ТаблицаСДанными,ИДКоллекции,ИДТаблицы,Родитель)
	
	//взял отсюда: http://www.forum.mista.ru/topic.php?id=588328
	
    МассивДобавляемыхРеквизитов	= Новый Массив;
    МассивУдаляемыхРеквизитов	= Новый Массив;
    КоллФормы = РеквизитФормыВЗначение(ИдКоллекции);

    Для каждого Колонка из КоллФормы.Колонки Цикл
		МассивУдаляемыхРеквизитов.Добавить(ИдКоллекции+"."+Колонка.Имя);
    КонецЦикла;
    
    Для каждого Колонка из ТаблицаСДанными.Колонки Цикл
 	   МассивДобавляемыхРеквизитов.Добавить(Новый РеквизитФормы(Колонка.Имя,Колонка.ТипЗначения,ИдКоллекции));
    КонецЦикла;
    
    ЭтаФорма.ИзменитьРеквизиты(МассивДобавляемыхРеквизитов,МассивУдаляемыхРеквизитов);
    ЗначениеВРеквизитФормы(ТаблицаСДанными,ИдКоллекции);
    
   // теперь динамически обновляем элементы формы
 
    ЭлементТаблица = Элементы.Найти(ИдТаблицы);
	
	ЭлементТаблица.Отображение					= ОтображениеТаблицы.Список;
    ЭлементТаблица.ТолькоПросмотр				= Истина;
    ЭлементТаблица.ИзменятьСоставСтрок			= Ложь ;
    ЭлементТаблица.ИзменятьПорядокСтрок			= Ложь;
	
	Для каждого Колонка из ТаблицаСДанными.Колонки Цикл
		Если Элементы.Найти(ИдТаблицы+Колонка.Имя)=Неопределено Тогда//если еще не создавали такой элемент
			ЭлКол = Элементы.Добавить(ИдТаблицы+Колонка.Имя,Тип("ПолеФормы"),ЭлементТаблица);
			ЭлКол.Вид			= ВидПоляФормы.ПолеВвода;
			ЭлКол.ПутьКДанным	= ИдКоллекции+"."+Колонка.Имя;
		КонецЕсли;	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Тест();
	
КонецПроцедуры

&НаКлиенте
Процедура Тест()
	
	//ФайлКаталога = "example.xml";
	//ОбновитьКаталог(ФайлКаталога);
	
КонецПроцедуры	

&НаКлиенте
Процедура ДеревоКаталогаПриАктивизацииСтроки(Элемент)
	
	ТекСтрока = Элементы.ДеревоКаталога.ТекущиеДанные;
	Если ТекСтрока=Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	НовОтбор = Новый Структура;
	НовОтбор.Вставить("Группа",ТекСтрока.ID);
	Элементы.Товары.ОтборСтрок = Новый ФиксированнаяСтруктура(НовОтбор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьИерархию(Команда)
	
	Элементы.Товары.ОтборСтрок = Неопределено;//заодно и все остальные отборы отключим
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьВыбранныеТовары(Команда)
	
	Если Элементы.Товары.ВыделенныеСтроки=Неопределено
		Или Элементы.Товары.ВыделенныеСтроки.Количество()=0 Тогда
		Предупреждение("Выделите строки, по которым хотите создать товары");
	Иначе
		Если Вопрос("Создать товары по выделенным строкам?",РежимДиалогаВопрос.ДаНет)=КодВозвратаДиалога.Да Тогда
			
			МассивСтрок = Новый Массив;
			Для Каждого ВыделеннаяСтрока Из Элементы.Товары.ВыделенныеСтроки Цикл
				МассивСтрок.Добавить(ВыделеннаяСтрока);
			КонецЦикла;	
			
			СоздатьВыбранныеТоварыНаСервере(МассивСтрок);
			
			Предупреждение("Создано");
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьВыбранныеТоварыНаСервере(ЗНАЧ МассивИндексовСтрок)

	МассивСтрок = Новый Массив;
	Для Каждого Строка Из МассивИндексовСтрок Цикл
		МассивСтрок.Добавить(Товары.НайтиПоИдентификатору(Строка));//здесь у нас Строка из ДанныеФормы
	КонецЦикла;
	
	МодульОбъекта().СоздатьТовары(МассивСтрок);
		
КонецПроцедуры

	


